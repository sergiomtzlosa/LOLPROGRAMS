BTW MADAFAKA!! GET SIN APROX WITH TAYLOR SERIES
HAI 1.2
CAN HAS STDIO?

BTW DA FACTORIAL CLCULATOR
HOW DUZ I FACTORIAL YR LOL
BOTH SAEM LOL AN 0
O RLY?
  YA RLY
	FOUND YR 1
  NO WAI
	FOUND YR PRODUKT OF LOL AN FACTORIAL DIFF OF LOL AN 1
OIC
IF U SAY SO

BTW THIZ IZ PI NUMBER
HOW DUZ I PINUMBERZ

	I HAS A PIADD ITZ 0.0
	I HAS A PISUB ITZ 0.0
	I HAS A ITR ITZ 0
	I HAS A ITRZ ITZ 20000
	I HAS A T1
	I HAS A T2

	IM IN YR LOOP
		T1 R QUOSHUNT OF 4.0 AN SUM OF 3.0 AN ITR
		T2 R QUOSHUNT OF 4.0 AN SUM OF 5.0 AN ITR
		PISUB R SUM OF PISUB AN T1
		PIADD R SUM OF PIADD AN T2
		ITR R SUM OF ITR AN 4.0
		
		BOTH SAEM ITR AN BIGGR OF ITR AN ITRZ, O RLY?
			YA RLY, GTFO
		OIC
	IM OUTTA YR LOOP

	FOUND YR SUM OF 4.0 AN DIFF OF PIADD AN PISUB
IF U SAY SO

BTW DA POW FUNCTION , pow(2,3) IZ 2^3=8
HOW DUZ I POW YR POWROCKET AN YR SEED

	I HAS A VALUE ITZ POWROCKET

    BOTH SAEM SEED AN 0
    O RLY?
      YA RLY
        FOUND YR POWROCKET
    OIC
    
	I HAS A INDEX ITZ 0
	IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX AN DIFF OF SEED AN 1
		VALUE R PRODUKT OF VALUE AN POWROCKET
	IM OUTTA YR LOOP
  
	FOUND YR VALUE
IF U SAY SO

BTW THIZ CALCULATE DA SIN(x) WIZ TAYLOR SERIES, MADAFAKA
HOW DUZ I SIN YR LOLVALUE

	I HAS A OPERAND ITZ QUOSHUNT OF PINUMBERZ AN 180.0
	MAEK OPERAND NUMBAR
	
	I HAS A RADIANS ITZ PRODUKT OF OPERAND AN LOLVALUE
	MAEK RADIANS NUMBAR
	
	I HAS A SOLUTION ITZ RADIANS
	MAEK SOLUTION NUMBAR

	I HAS A PRECISIONZ ITZ 10

	I HAS A INDEX
	INDEX R 1
	IM IN YR LOOP UPPIN YR INDEX WILE BOTH SAEM INDEX AN SMALLR OF INDEX AN PRECISIONZ
		
		BOTH SAEM INDEX AN BIGGR OF INDEX AN 1 
		O RLY?
		  YA RLY

			I HAS A MULTERM ITZ SUM OF PRODUKT OF 2 AN INDEX AN 1
			MAEK MULTERM NUMBR
	
			I HAS A POWOPERAND ITZ POW RADIANS MULTERM
			MAEK POWOPERAND NUMBAR

			BTW VISIBLE RADIANS "^" MULTERM " = " POWOPERAND

			I HAS A ANOTHEROPERAND ITZ FACTORIAL MULTERM 
			MAEK ANOTHEROPERAND NUMBAR

			I HAS A RESULT ITZ QUOSHUNT OF POWOPERAND AN ANOTHEROPERAND	
			MAEK RESULT NUMBAR
			
			BOTH SAEM MOD OF INDEX AN 2 AN 0
			O RLY?
  		  	YA RLY
				SOLUTION R SUM OF SOLUTION AN RESULT
		  	NO WAI
				SOLUTION R DIFF OF SOLUTION AN RESULT
			OIC
		OIC
	IM OUTTA YR LOOP

	FOUND YR SOLUTION
IF U SAY SO

BTW THIZ CALCULATE DA COS(x) WIZ TAYLOR SERIES, MADAFAKA
HOW DUZ I COS YR LOLVALUE

    I HAS A OPERAND ITZ QUOSHUNT OF PINUMBERZ AN 180.0
	MAEK OPERAND NUMBAR
	
	I HAS A RADIANS ITZ PRODUKT OF OPERAND AN LOLVALUE
	MAEK RADIANS NUMBAR
    
    I HAS A SOLUTION ITZ 0.0
	MAEK SOLUTION NUMBAR

	I HAS A PRECISIONZ ITZ 10
	I HAS A INDEX ITZ 0
    
    I HAS A TEMPINDEX ITZ 1
    I HAS A SIGN ITZ 1
	IM IN YR LOOP UPPIN YR INDEX WILE DIFFRINT INDEX AN BIGGR OF INDEX AN PRECISIONZ
        
        DIFFRINT TEMPINDEX AN BIGGR OF TEMPINDEX AN PRECISIONZ
        O RLY?
          YA RLY
  
            I HAS A FIRSTOPERAND ITZ PRODUKT OF SIGN AN POW RADIANS TEMPINDEX
    
            I HAS A SECONDOPERAND ITZ FACTORIAL TEMPINDEX

            I HAS A CALCULATION ITZ QUOSHUNT OF FIRSTOPERAND AN SECONDOPERAND
        
            SOLUTION R SUM OF SOLUTION AN CALCULATION
            SIGN R PRODUKT OF SIGN AN -1 
            TEMPINDEX R SUM OF TEMPINDEX AN 2
            
          NO WAI 
            FOUND YR SOLUTION
        OIC
        
    IM OUTTA YR LOOP
IF U SAY SO

VISIBLE "SELECT OPERATION TO PERFORM: "
VISIBLE "1 - SIN"
VISIBLE "2 - COSIN"
VISIBLE "DEFAULT OPTION 1:"

I HAS A OPTION
GIMMEH OPTION

BOTH SAEM OPTION AN ""
O RLY?
    YA RLY
         OPTION R "1"
OIC

VISIBLE "TYPE AN ANGLE: "
I HAS A INPUTVAR
MAEK INPUTVAR NUMBAR
GIMMEH INPUTVAR

OPTION, WTF?
  OMG "1"
    I HAS A MAX ITZ 90
    DIFFRINT MAX AN BIGGR OF MAX AN INPUTVAR
    O RLY?
        YA RLY
            VISIBLE "DA ANGLE MUST BE LESS THAN 90ยบ"
        NO WAI
            VISIBLE "SIN(" INPUTVAR ") = " SIN INPUTVAR
    OIC
    GTFO
  OMG "2"
    I HAS A MAX ITZ 90
    DIFFRINT MAX AN BIGGR OF MAX AN INPUTVAR
    O RLY?
        YA RLY
            VISIBLE "DA ANGLE MUST BE LESS THAN 90ยบ"
        NO WAI
            VISIBLE "COS(" INPUTVAR ") = " COS INPUTVAR
    OIC
    GTFO
  OMGWTF
OIC
  
KTHXBYE